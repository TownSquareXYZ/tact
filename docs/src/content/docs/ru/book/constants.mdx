---
title: Константы
description: Неизменяемые значения, которые нельзя изменить путем переназначения
---

Константы в Tact могут быть немного более продвинутыми, чем в популярных языках: они могут быть виртуальными и абстрактными. Смарт-контракты часто должны реализовывать несколько признаков, и иногда вам нужно настроить некоторые из них во время компиляции. Конструкторы в признаках запрещены из-за их непредсказуемого поведения. Поэтому вместо этого нам приходится использовать константы и поля, чтобы передавать им значения. Реализация значений и констант для всех признаков — задача основного контракта.

## Простая константа

Начнем с простой константы. Это значение, которое определяется во время компиляции и не может быть изменено. Вы можете определить константу на верхнем уровне или внутри контракта/признака. Давайте определим константу на верхнем уровне:

```tact
const MY_CONSTANT: Int = 42;
```

Аналогично для признаков и контрактов:

```tact
trait MyTrait {
    const MY_CONSTANT: Int = 42;
}

contract MyContract {
    const MY_CONSTANT: Int = 42;
}
```

## Виртуальные и абстрактные константы {#virtual-and-abstract-constants}

Виртуальные константы — это константы, которые могут быть определены в признаке, но изменены в контракте. Это полезно, когда вам нужно настроить некоторые признаки во время компиляции. Давайте определим виртуальную константу и абстрактную:

```tact
trait MyTrait {
    virtual const MY_FEE: Int = ton("1.0");
}

trait MyAbstractTrait {
    abstract const MY_DEV_FEE: Int;
}
```

Теперь вы можете перезаписать значения по умолчанию в контракте:

```tact
contract MyContract with
    MyTrait,
    MyAbstractTrait, // trailing comma is allowed
{
    override const MY_FEE: Int = ton("0.5");
    override const MY_DEV_FEE: Int = ton("1000");
}
```

Это может быть очень полезно, чтобы помочь компилятору получить некоторые значения во время компиляции, например, вы можете включать и отключать функции без необходимости изменять код и не тратить газ впустую.

```tact
trait Treasure {
    virtual const ENABLE_TIMELOCK: Bool = true;

    receive("Execute") {
        if (self.ENABLE_TIMELOCK) {
            //
            // Этот ветвь будет удалена во время компиляции, если ENABLE_TIMELOCK равно false
            //
        }
    }
}

contract MyContract with Treasure {
    override const ENABLE_TIMELOCK: Bool = false;
}
```
