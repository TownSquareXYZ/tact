---
title: Опциональные значения
---

Как упоминалось в [обзоре системы типов](/ru/book/types#optionals), все [примитивные типы](/ru/book/types#primitive-types), [структуры (Structs)](/ru/book/structs-and-messages#structs) и [сообщения (Messages)](/ru/book/structs-and-messages#messages) могут быть nullable. То есть, они не обязательно содержат какое-либо значение, за исключением `null{:tact}` - специального значения, которое представляет преднамеренное отсутствие любого другого значения.

[Переменные](/ru/book/statements#let) или поля [Structs](/ru/book/structs-and-messages#structs) и [Messages](/ru/book/structs-and-messages#messages), которые могут содержать значение `null{:tact}`, называются "опциональными значениями". Они полезны для уменьшения размера состояния, если переменная используется не обязательно.

Вы можете сделать любую переменную или поле опциональным, добавив знак вопроса (`?{:tact}`) после объявления её типа. Единственными исключениями являются [`map<K, V>{:tact}`](/ru/book/maps) и [`bounced<Msg>{:tact}`](/ru/book/bounced), для которых нельзя сделать опциональными их внутренние типы ключей/значений (в случае map) или внутренние [сообщения](/ru/book/structs-and-messages#messages) (в случае bounced).

Опциональные переменные или опциональные поля, которые не определены, по умолчанию имеют значение `null{:tact}`. Вы не можете получить к ним доступ без предварительной проверки на `null{:tact}`. Но если вы уверены, что в данный момент они не имеют значения `null{:tact}`, используйте [оператор ненулевого утверждения `!!{:tact}`](/ru/book/operators#unary-non-null-assert) для доступа к их значению.

Попытка получить доступ к значению опциональной переменной или опционального поля без использования [`!!{:tact}`](/ru/book/operators#unary-non-null-assert) или без предварительной проверки на `null{:tact}` приведет к ошибке компиляции, если компилятор может это отследить, а если нет - к исключению с [кодом завершения 128](/ru/book/exit-codes#128): `Null reference exception`.

Пример использования опциональных значений:

```tact
struct StOpt {
    opt: Int?; // Int или null
}

message MsOpt {
    opt: StOpt?; // Обратите внимание, как структура StOpt используется в этом определении
}

contract Optionals {
    opt: Int?;
    address: Address?;

    init(opt: Int?) { // опциональные значения в качестве параметров
        self.opt = opt;
        self.address = null; // явное значение null
    }

    receive(msg: MsOpt) {
        let opt: Int? = 12; // Определение новой переменной
        if (self.opt != null) { // явная проверка
            self.opt = opt!!; // использование !! так как мы знаем, что значение opt не равно null
        }
    }
}
```
